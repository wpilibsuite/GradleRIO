plugins {
    id 'com.gradle.build-scan' version '1.9.1'
    id 'com.gradle.plugin-publish' version '0.9.9'
    id 'me.champeau.buildscan-recipes' version '0.2.0'
    id 'java-gradle-plugin'
    id 'idea'
    id 'maven-publish'
    id 'groovy'
}

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenLocal()
}

dependencies {
//    compile 'jaci.gradle:EmbeddedTools:2018.05.05'
    compile 'gradle.plugin.jaci.gradle:DeployTools:2018.06.18'
    compile 'de.undercouch:gradle-download-task:3.1.2'
    compile 'com.google.code.gson:gson:2.2.4'
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

buildScanRecipes {
    recipes 'git-commit', 'git-status', 'travis-ci', 'disk-usage', 'gc-stats'
}

group = "edu.wpi.first"
archivesBaseName = "GradleRIO"
version = "2018.06.21"

pluginBundle {
    website = 'https://github.com/wpilibsuite/GradleRIO'
    vcsUrl = 'https://github.com/wpilibsuite/GradleRIO'
    description = 'Managing FRC projects, the Gradle way (2019+)'
    tags = ['frc', 'wpilib', 'gradlerio']

    plugins {
        gradleRio {
            id = "${project.group}.${project.archivesBaseName}"
            displayName = 'GradleRIO'
        }
    }
}

publishing {
    publications {
        plugin(MavenPublication) {
            artifact(jar) {
                if (project.hasProperty('localPublish')) {
                    classifier null
                    groupId 'gradle.plugin.' + group
                }
            }
        }
    }
}

gradlePlugin {
    plugins {
        gradleRio {
            id = 'edu.wpi.first.GradleRIO'
            implementationClass = 'edu.wpi.first.gradlerio.GradleRIOPlugin'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
}
