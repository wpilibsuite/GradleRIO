plugins {
    id 'com.gradle.build-scan' version '1.9.1'
    id 'com.gradle.plugin-publish' version '0.9.9'
    id 'java-gradle-plugin'
    id 'idea'
    id 'maven-publish'
    id 'groovy'
}

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenLocal()
}

dependencies {
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'gradle.plugin.edu.wpi.first:gradle-cpp-vscode:0.5.0'

    compile project(':ToolchainPlugin')

    compile 'com.jcraft:jsch:0.1.53'

    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile gradleTestKit()
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

archivesBaseName = "GradleRIO"

allprojects {
    group = "edu.wpi.first"
    version = "2019.1.1-beta-3-pre-2"

    if (project.hasProperty('publishVersion')) {
        version = project.publishVersion
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

pluginBundle {
    website = 'https://github.com/wpilibsuite/GradleRIO'
    vcsUrl = 'https://github.com/wpilibsuite/GradleRIO'
    description = 'Managing FRC projects, the Gradle way (2019+)'
    tags = ['frc', 'wpilib', 'gradlerio']

    plugins {
        gradleRio {
            id = "${project.group}.${project.archivesBaseName}"
            displayName = 'GradleRIO'
        }
    }
}

publishing {
    publications {
        plugin(MavenPublication) {
            artifact(jar) {
                if (project.hasProperty('localPublish')) {
                    classifier null
                    groupId 'gradle.plugin.' + group
                }
            }
        }
    }
}

gradlePlugin {
    plugins {
        gradleRio {
            id = 'edu.wpi.first.GradleRIO'
            implementationClass = 'edu.wpi.first.gradlerio.GradleRIOPlugin'
        }
    }
}

apply from: 'versionupdates.gradle'

tasks.register('PatchExamples') {
    doLast {
        String regex = "(id\\s*?[\\\"|\\']edu\\.wpi\\.first\\.GradleRIO[\\\"|\\'].*?version\\s*?[\\\"|\\'])(.+?)([\\\"|\\'])";

        def examplesFolder = file("$rootDir/examples")
        examplesFolder.eachFile { File file ->
            if (file.isDirectory() && file.name != '_archived') {
                def buildGradleFile = new File(file, 'build.gradle')
                if (buildGradleFile.exists() && buildGradleFile.isFile()) {
                    def text = buildGradleFile.text
                    text = text.replaceAll(regex, "id \"edu.wpi.first.GradleRIO\" version \"${version}\" ")
                    buildGradleFile.text = text
                }
            }
        }
    }
}

wrapper {
    gradleVersion = '4.9'
}
