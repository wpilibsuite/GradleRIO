plugins {
    id "com.gradle.plugin-publish" version "1.2.1"
    id 'java-gradle-plugin'
    id 'idea'
    id 'maven-publish'
    id 'java'
    id 'groovy'
}

repositories {
    maven {
        url = "https://plugins.gradle.org/m2/"
    }
    mavenLocal()
}

dependencies {
    api 'com.google.code.gson:gson:2.8.6'

    api 'org.wpilib:native-utils:2027.1.1'

    api 'de.undercouch:gradle-download-task:4.1.2'

    testImplementation('org.spockframework:spock-core:2.4-M6-groovy-4.0') {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testImplementation gradleTestKit()
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

base {
    archivesName = "GradleRIO"
}

java {
    sourceCompatibility = 17
    targetCompatibility = 17
}

allprojects {
    group = "org.wpilib"

    if (project.hasProperty('publishVersion')) {
        version = project.publishVersion
    }

    tasks.withType(Javadoc) {
        options.addBooleanOption('Xdoclint:all,-missing', true)
    }

    tasks.withType(JavaCompile) {
      options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
  }
}

gradlePlugin {
    website = 'https://github.com/wpilibsuite/GradleRIO/tree/2027'
    vcsUrl = 'https://github.com/wpilibsuite/GradleRIO/tree/2027'
    plugins {
        gradleRio {
            id = 'org.wpilib.GradleRIO'
            displayName = 'GradleRIO'
            implementationClass = 'org.wpilib.gradlerio.GradleRIOPlugin'
            description = 'Managing FRC projects, the Gradle way (2027+)'
            tags = ['frc', 'wpilib', 'gradlerio', 'systemcore']
        }
    }
}

apply from: 'versionupdates.gradle'

def examplesFolder = file("$rootDir/testing")

tasks.register('PatchExamples') {
    doLast {
        String regex = "(id\\s*?[\\\"|\\']org\\.wpilib\\.GradleRIO[\\\"|\\'].*?version\\s*?[\\\"|\\'])(.+?)([\\\"|\\'])";

        examplesFolder.eachFile { File file ->
            if (file.isDirectory() && file.name != '_archived') {
                def buildGradleFile = new File(file, 'build.gradle')
                if (buildGradleFile.exists() && buildGradleFile.isFile()) {
                    def text = buildGradleFile.text
                    text = text.replaceAll(regex, "id \"org.wpilib.GradleRIO\" version \"${version}\"")
                    buildGradleFile.text = text
                }
            }
        }
    }
}

task zipExamples(dependsOn: PatchExamples)

examplesFolder.eachFile { File file ->
    if (file.isDirectory() && file.name != '_archived') {
        task "zipExample${file.name}"(type: Zip) {
            from(file) {
                archiveFileName = "${file.name}.zip"
                exclude 'build/'
                exclude '.gradle/'
            }

            zipExamples.dependsOn it
            it.dependsOn PatchExamples
        }
    }
}

jar.finalizedBy zipExamples

wrapper {
    gradleVersion = '9.2.0'
    distributionType = Wrapper.DistributionType.BIN
}
