// Note 'apply false' for each plugin. This ensures they aren't applied to the root project (here), 
// but it makes the plugins available for subprojects.
plugins {
    // GradleRIO is used for code intended to go on robots, which is why it's only applied to the 'robot' project.
    id "jaci.openrio.gradle.GradleRIO" version "2018.01.07" apply false

    // EmbeddedTools is the backing library for GradleRIO, and includes everything that's non-frc specific, which
    // is why we use it for our coprocessor project.
    id "jaci.gradle.EmbeddedTools" version "2017.12.23" apply false
}

// Using the 'idea' plugin allows us to use IntelliJ IDEA. IntelliJ works out-of-the-box with multiproject builds
// and so it's very, very easy to integrate with GradleRIO and EmbeddedTools.
// We need to apply 'idea' to all projects, including the root project (here).
allprojects {
    apply plugin: "idea"
}

// Setup the configuration for all sub-projects. This is a shortcut for manually applying the configuration
// in each subproject individually.
subprojects {
    apply plugin: "java"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}